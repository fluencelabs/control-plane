aqua Spell declares get_string, spell_log, get_counter, deal_log, list_push, list_remove, list_get

import Spell from "@fluencelabs/spell/spell_service.aqua"
import Op from "@fluencelabs/aqua-lib/builtin.aqua"
import RunConsoleManyAny from "../debug.aqua"
import SpellId, DealId from "../types.aqua"
import OpExt, Json from "peer.aqua"

func spell_log(msg: ⊤):
    co RunConsoleManyAny.print("decider", msg)
    msg_str <- Json.stringify(msg)
    Spell "spell" 
    Spell.store_log(msg_str)

func deal_log(deal_id: DealId, msg: ⊤):
    title <- OpExt.concat_strings("decider deal_id=", deal_id, ": ")
    co RunConsoleManyAny.print(title, msg)
    msg_str <- Json.stringify(msg)
    msg_full <- Op.concat_strings(title, msg_str)
    Spell "spell" 
    Spell.store_log(msg_full)

-- Wrapper over spell.get_string to get a string from spell's KV with reduced amount of if-else's
func get_string(spell_id: SpellId, key: string) -> ?string:
    Spell spell_id

    result: *string

    value <- Spell.get_string(key)
    if !value.success:
        spell_log(["get_string failed", key, value.error])
    else:
        if !value.absent:
            result <<- value.value

    <- result

func get_counter(spell_id: SpellId) -> u32:
    Spell spell_id
    result: *u32
    counter <- Spell.get_u32("hw_counter")
    if counter.success:
        result <<- counter.value
    else:
        result <<- 0
    <- result!

func list_get(spell_id: SpellId, key: string) -> ?[]string:
    Spell spell_id
    list: *[]string
    result <- Spell.list_get_strings(key)
    if !result.success:
        spell_log(["can't get list by key", key, "error:", result.error])
    else:
        list <<- result.value
    <- list

func list_push(spell_id: SpellId, key: string, val: ⊤):
    Spell spell_id
    str <- Json.stringify(val)
    result <- Spell.list_push_string(key, str)
    if !result.success:
        spell_log(["can't store value in list by key", key, "value", val, "error:", result.error])

func list_remove(spell_id: SpellId, key: string, val: ⊤):
    Spell spell_id
    str <- Json.stringify(val)
    result <- Spell.list_remove_string(key, str)
    if !result.success:
        spell_log(["can't remove value in list by key", key, "value", val, "error:", result.error])