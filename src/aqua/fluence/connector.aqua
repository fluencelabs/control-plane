aqua Connector declares *

import CUID from "@fluencelabs/aqua-lib/workers.aqua"
import DealId, TxHash, Deal from "../types.aqua"


data DealInfo:
    -- For comparison, use DEAL_STATUS_* from consts.aqua
    status: string
    unit_ids: []CUID
    app_cid: string

func info_to_deal(deal_id: DealId, deal_info: DealInfo) -> Deal:
    <- Deal(
        deal_id = deal_id, 
        status = deal_info.status,
        app_cid = deal_info.app_cid,
        unit_ids = deal_info.unit_ids
       )

data DealResult:
    success: bool
    error: ?string
    deal_id: DealId
    deal_info: ?DealInfo

data TxInfo:
    deal_id: DealId 
    tx_hash: string

data TxReceipt:
    block_number: string
    tx_hash: TxHash

data TxResult:
    success: bool
    error: ?string
    -- Should be either 'pending', 'failed' or 'ok'
    -- For comparison, use TX_STATUS_* from consts.aqua
    status: string
    -- Should be present when status isn't `pending`
    receipt: ?TxReceipt

service ChainConnector("connector"):
  -- Register a worker on chain
  register_worker(deal_id: string, worker_id: string, unit_ids: []CUID) -> TxHash
  -- Get the list of current deals
  get_deals() -> []DealResult
  -- Get transaction receipts for the given transaction hashes
  get_tx_receipts(tx_hashes: []TxHash) -> []TxResult
