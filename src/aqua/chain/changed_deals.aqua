module ChangedDeals declares poll_deal_changes_batch

import Spell, TriggerConfig from "@fluencelabs/spell/spell_service.aqua"

import deal_log, spell_log, get_string from "../fluence/spell.aqua"
import Json from "../fluence/peer.aqua"
import ChainInfo from "../chain/chain.aqua"
import JsonJoinedDeal, JsonDealState, DealState, store_deal_state, get_joined_deals from "../decider/deal_storage.aqua"
import SpellId, DealId from "../types.aqua"

import DealInfo, DealChangesReq, FluenceAuroraConnector, DealChangedResult from "services.aqua"

use "../chain/blocks.aqua" as Blocks
use "../hex.aqua" as Hex

func get_deal_changes(spell_id: SpellId, api_endpoint: string) -> ?[]DealChangedResult:
    changes: ?[]DealChangedResult

    deals <- get_joined_deals(spell_id)
    if deals != nil:
        -- Poll for the last logs about deal changes
        poll_result <- FluenceAuroraConnector.poll_deal_changes(api_endpoint, deals!)
        if poll_result.success:
            spell_log(spell_id, ["deal changes from poll:", poll_result.changes.length])
            changes <<- poll_result.changes
        else:
            spell_log(spell_id, ["error retrieving deal changes from chain", poll_result.error!])

    <- changes

-- Update app_cid of the deal on the corresponding worker spell
func update_worker(spell_id: SpellId, change: DealChangedResult):
    deal_id = change.deal_info.deal_id
    worker_id = change.deal_info.worker_id
    app_cid <- Json.stringify(change.log!.info.app_cid)
    deal_log(spell_id, deal_id, ["sending the latest update to the worker", worker_id, app_cid])
    on worker_id:
        -- stringify app_cid to be able to use it as an argument of a spell
        Spell "worker-spell"
        Spell.set_string("worker_def_cid", app_cid)

-- Move left boundary of the deal's block range to be `min(right boundary, latest_block) + 1`
func move_left_boundary(spell_id: SpellId, deal_id: DealId, right_boundary: string, latest: string):
    spell_log(spell_id, ["move_left_boundary call Hex.min", right_boundary, latest])
    min <- Hex.min(spell_id, right_boundary, latest)
    if min != nil:
        left <- Hex.inc(spell_id, min!)
        if left != nil:
            store_deal_state(spell_id, deal_id, DealState(left_boundary = left!))

func poll_deal_changes_batch(spell_id: SpellId, chain: ChainInfo, latest_block: string):
    Spell spell_id

    changes <- get_deal_changes(spell_id, chain.api_endpoint)
    if changes != nil:
        -- Update all deals with new info
        for change <- changes!:
            deal_id = change.deal_info.deal_id

            if change.success:
                deal_log(spell_id, deal_id, ["found a deal changed log on block", change.log!.block_number])

                -- Update app_cid of the deal on the corresponding worker spell
                update_worker(spell_id, change)

                -- Move block range so that log's block becomes new left boundary
                log_block = change.log!.block_number
                store_deal_state(spell_id, deal_id, DealState(left_boundary = log_block))
            else:
                deal_log(spell_id, deal_id, ["error retrieving deal change", change.error])

        -- After we have processed all poll logs, we can conclude that we have seen
        -- all logs from all blocks in range [deal.left, min(change.right, latest_block)]
        -- so we move `left_boundary` for each deal to the next block after `min`
        -- so that on the next iteration, poll will request logs from `min(change.right, latest_block) + 1`
        for change <- changes! par:
            deal_id = change.deal_info.deal_id
            move_left_boundary(spell_id, deal_id, change.right_boundary, latest_block)
