aqua DealStorage declares *

import Spell from "@fluencelabs/spell/spell_service.aqua"

import FAILED_DEALS, JOINED_DEALS, removed_state_key from "./consts.aqua"
import Json from "../fluence/peer.aqua"
import spell_log, get_string, deal_log, list_push from "../fluence/spell.aqua"
import DealId, SpellId, WorkerId, Block from "../types.aqua"

import DealInfo, DealMatched from "services.aqua"

data DealState:
    left_boundary: string

service JsonDealState("json"):
    parse(str: string) -> DealState

func store_deal_state(decider_id: SpellId, deal_id: DealId, state: DealState):
    Spell decider_id

    json <- Json.stringify(state)
    Spell.set_string(deal_id, json)

func get_deal_state(decider_id: SpellId, deal_id: string, key: string) -> ?DealState:
    Spell decider_id
    state: *DealState

    json <- Spell.get_string(key)
    if json.success && !json.absent:
        try:
            state <- JsonDealState.parse(json.value)
        catch e:
            deal_log(decider_id, deal_id, ["failed to parse deal state (key = ", key, ") from json", e])
    else:
        deal_log(decider_id, deal_id, ["deal state (key = ", key, ") not found:", json.error])

    <- state

func remove_deal_state(spell_id: SpellId, deal_id: DealId):
    Spell spell_id
    Spell.remove_key(deal_id)

-- Deal state for polling removed events
-- TODO: refactor how to we store deals state (incl. joined_deals)
func store_deal_state_removed(spell_id: SpellId, deal_id: DealId, left_boundary: string):
    Spell spell_id
    state = DealState(left_boundary = left_boundary)
    json <- Json.stringify(state)
    Spell.set_string(removed_state_key(deal_id), json)

func get_deal_state_removed(spell_id: SpellId, deal_id: DealId) -> ?DealState:
    <- get_deal_state(spell_id, deal_id, removed_state_key(deal_id))

func remove_deal_state_removed(spell_id: SpellId, deal_id: DealId):
    Spell spell_id
    Spell.remove_key(removed_state_key(deal_id))

-- Info about joined deals
data JoinedDeal:
    deal_id: DealId
    worker_id: WorkerId

service JsonJoinedDeal("json"):
    parse(str: string) -> JoinedDeal

func store_joined_deal(decider_id: SpellId, deal_id: DealId, worker_id: WorkerId):
    Spell decider_id
    deal = JoinedDeal(deal_id = deal_id, worker_id = worker_id)
    msg <- Json.stringify(deal)
    Spell.list_push_string(JOINED_DEALS, msg)

func remove_joined_deal(spell_id: SpellId, joined_deal: JoinedDeal):
    Spell spell_id
    str <- Json.stringify(joined_deal)
    Spell.list_remove_string(JOINED_DEALS, str)

func get_joined_deals(spell_id: SpellId) -> []JoinedDeal:
    Spell spell_id

    joined_deals: *JoinedDeal

    list <- Spell.list_get_strings(JOINED_DEALS)
    if !list.success:
        spell_log(spell_id, ["can't restrive joined deals", list.error])
    else:
        for joined_deal_str <- list.value:
            try:
                joined_deals <- JsonJoinedDeal.parse(joined_deal_str)
            catch e:
                spell_log(spell_id, ["error parsing JoinedDeal", joined_deal_str, e])

    <- joined_deals

func store_deal(spell_id: SpellId, deal_id: DealId, worker_id: WorkerId, block: Block):
    try:
        store_joined_deal(spell_id, deal_id, worker_id)
        store_deal_state(spell_id, deal_id, DealState(left_boundary = block))
        store_deal_state_removed(spell_id, deal_id, block)
        deal_log(spell_id, deal_id, "deal state saved to kv")
    catch e:
        deal_log(spell_id, deal_id, ["cannot store deal state, deal join failed", e.message])

func remove_deal_storage(spell_id: string, deal_id: string, worker_id: string):
    try:
        remove_joined_deal(spell_id, JoinedDeal(deal_id = deal_id, worker_id = worker_id))
        remove_deal_state(spell_id, deal_id)
        remove_deal_state_removed(spell_id, deal_id)
    catch e:
        deal_log(spell_id, deal_id, ["couldn't remove the deal", e])

data InstallationFailed:
    log: DealMatched

data TxFailed:
    tx_hash: ?string
    block_number: ?string

data FailedDealError:
    type: string
    content: âŠ¤

-- Deal for which we weren't able to create a worker
data FailedDeal:
   deal_id: DealId
   message: string
   payload: FailedDealError


service JsonFailedDeal("json"):
    parse(str: string) -> FailedDeal

func store_installation_failed_deal(spell_id: SpellId, deal_id: DealId, log: DealMatched, error: string):
    deal = FailedDeal(
        deal_id = deal_id,
        message = error,
        payload = FailedDealError(
            type = "InstallationFailed",
            content = InstallationFailed(log = log)
        )
    )
    list_push(spell_id, FAILED_DEALS, deal)

func store_tx_failed_deal(spell_id: SpellId, deal_id: DealId, error: string, tx_hash: ?string, block_number: ?string):
    deal = FailedDeal(
        deal_id = deal_id,
        message = error,
        payload = FailedDealError(
            type = "TxFailed",
            content = TxFailed(tx_hash = tx_hash, block_number = block_number)
        )
    )
    list_push(spell_id, FAILED_DEALS, deal)

func get_failed_deals(spell_id: SpellId) -> []FailedDeal, []string:
    Spell spell_id

    deals: *FailedDeal
    errors: *string

    result <- Spell.list_get_strings(FAILED_DEALS)
    if !result.success:
        errors <<- result.error
    else:
        for str <- result.value:
            try:
                deals <<- JsonFailedDeal.parse(str)
            catch e:
                errors <- Json.stringify(["can't parse FaileDeal", e, str])
    <- deals, errors
