module Poll declares main

export main

import SpellId from "../types.aqua"
import ChainInfo from "../chain/chain.aqua"
import poll_new_deals from "../chain/new_deals.aqua"
import poll_deal_changes_batch from "../chain/changed_deals.aqua"
import get_joined_deals from "../decider/deal_storage.aqua"
import poll_mailbox from "../decider/mailbox.aqua"
import poll_txs from "./register_worker.aqua"
import store_sync_info from "./sync_info.aqua"
import poll_deal_statuses from "../chain/deal_status.aqua"
import WorkerSettings from "./fluence/worker.aqua"

use "../chain/blocks.aqua" as Blocks

func main(spell_id: SpellId, chain: ChainInfo, settings: WorkerSettings):
    -- Get the latest block number from chain
    latest_block <- Blocks.get_latest(spell_id, chain)

    if latest_block != nil:
        -- load deals that are already joined
        -- poll new deals uses it to learn if deal was already joined
        -- poll deal changes retrieves changes for these deals
        joined_deals <- get_joined_deals(spell_id)

        try:
            -- Find new deals and create workers
            poll_new_deals(spell_id, chain, joined_deals, latest_block!, settings)

        try:
            -- Update existing deals
            poll_deal_changes_batch(spell_id, chain, joined_deals, latest_block!, settings)

        try:
            poll_deal_statuses(spell_id, chain.api_endpoint, joined_deals)

        try:
            poll_txs(spell_id, chain.api_endpoint)

        try:
            store_sync_info(spell_id, latest_block!)

        try:
            poll_mailbox(spell_id)

