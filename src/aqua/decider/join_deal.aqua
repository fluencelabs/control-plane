module JoinDeal declares join_deal

import PeerSpell from "@fluencelabs/spell/api.aqua"
import Spell, TriggerConfig from "@fluencelabs/spell/spell_service.aqua"

-- import Worker from "../fluence/peer.aqua"
import deal_log from "../fluence/spell.aqua"
import get_worker_settings, WorkerArgs, WorkerSettings from "../fluence/worker.aqua"
import DealState, store_joined_deal, store_deal_state from "../decider/deal_storage.aqua"
import DealId, SpellId, CID from "../types.aqua"

import DealCreated from "services.aqua"

use "../fluence/worker.aqua" as Worker

func install_deal_spell(deal_id: DealId, cid: CID, settings: WorkerSettings) -> string:
    args = WorkerArgs(deal_id = deal_id, worker_def_cid = cid, ipfs = settings.ipfs)
    worker_spell_id <- PeerSpell.install(settings.script, args, settings.config)
    <- worker_spell_id

-- TODO: what should happen if join_deal has failed? We might never receive it again
--       Probably should store failed deals, and then retry them a few times?
func join_deal(spell_id: SpellId, block: string, deal_id: DealId, app_cid: CID):
    log = (msg: âŠ¤):
        deal_log(spell_id, deal_id, msg)

    log(["joining a deal from_block", block])
    worker_id <- Worker.create(spell_id, deal_id)
    if worker_id != nil:
        settings <- get_worker_settings(spell_id)
        if settings != nil:
            on worker_id!:
                try:
                    deal_spell <- install_deal_spell(deal_id, app_cid, settings!)
                    log(["created deal spell", deal_spell])
                    try:
                        store_joined_deal(spell_id, deal_id, deal_spell, worker_id!)
                        store_deal_state(spell_id, deal_id, DealState(left_boundary = block))
                        log("joined the deal")
                    catch e:
                        log(["cannot store deal state, deal join failed", e.message])
                catch e:
                    log(["cannot create deal spell, deal join failed", e.message])
        else:
            log("error reading worker settings, deal join failed")
